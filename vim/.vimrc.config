scriptencoding utf-8

" 標準添付プラグインを読み込まない {{{

let g:loaded_2html_plugin = 1
let g:loaded_dvorak_plugin = 1
let g:loaded_getscript = 1
let g:loaded_getscriptPlugin = 1
let g:loaded_logiPat = 1
let g:loaded_matchparen = 1
" let g:loaded_netrw = 1
" let g:loaded_netrwFileHandlers = 1
" let g:loaded_netrwPlugin = 1
" let g:loaded_netrwSettings = 1
let g:loaded_rrhelper = 1
let g:loaded_spellfile_plugin = 1
let g:loaded_gzip = 1
let g:loaded_tar = 1
let g:loaded_tarPlugin = 1
let g:loaded_vimball = 1
let g:loaded_vimballPlugin = 1
let g:loaded_zip = 1
let g:loaded_zipPlugin = 1
let g:did_install_default_menus = 1
let g:skip_loading_mswin = 1
let g:did_install_syntax_menu = 1

let g:no_gvimrc_example = 1
let g:no_vimrc_example = 1

" }}}

" KaoriYaのプラグインを読み込まない {{{

let g:plugin_autodate_disable = 1
" let g:plugin_cmdex_disable = 1
let g:plugin_dicwin_disable = 1
let g:plugin_hz_ja_disable = 1
let g:plugin_scrnmode_disable = 1
" let g:plugin_verifyenc_disable = 1

" }}}

" netrwの設定 {{{

" ls -lのように表示する
let g:netrw_liststyle = 1

" バナーを表示しない
let g:netrw_banner = 0

" ファイルサイズを読みやすくする
let g:netrw_sizestyle = 'H'

" タイムスタンプの表示を変更する
let g:netrw_timefmt = '%Y/%m/%d %H:%M'

" キャッシュを使用しない
let g:netrw_fastbrowse = 0

" }}}

" デフォルトシェルをBashにする
" :help sh.vim
let g:is_bash = 1

" Markdown内での強調表示
" http://mattn.kaoriya.net/software/vim/20140523124903.htm
let g:markdown_fenced_languages = [
      \ 'css',
      \ 'diff',
      \ 'erb=eruby',
      \ 'html',
      \ 'javascript',
      \ 'js=javascript',
      \ 'json',
      \ 'ruby',
      \ 'sass',
      \ 'scss',
      \ 'sh',
      \ 'typescript',
      \ 'ts=typescript',
      \ 'vim',
      \ 'xml',
      \ ]

if exists('+shellslash')
  set shellslash
endif

if !has('gui_running')
  " 256色にする
  set t_Co=256
endif

if has('vertsplit')
  " カレントウィンドウの右に分割する
  set splitright
endif

if !has('gui_running')
  " マウス操作を受け付けない
  set mouse=
endif

if has('guess_encode')
  " 文字コードの自動判別
  set fileencodings=guess,ucs-bom,iso-2022-jp-3,utf-8,euc-jp,cp932
elseif has('multi_byte')
  " 開いたファイルに合っているものを順番に試す

  " https://github.com/Shougo/shougo-s-github/blob/b12435cdded41c7d77822b2a0a97beeab09b8d2c/vim/rc/init.rc.vim#L28-L29
  " set fileencodings=ucs-bom,iso-2022-jp-3,utf-8,euc-jp,cp932

  " http://www.tooyama.org/vim-2.html
  " set fileencodings=iso-2022-jp,cp932,sjis,euc-jp,utf-8

  " https://ravelll.hatenadiary.jp/entry/2015/08/20/140710
  " set fileencodings=utf-8,euc-jp,sjis,cp932,iso-2022-jp

  " 順番的にこれでないと解析できないが、マルチバイトのないファイルでのコードがiso-2022-jpになってしまう
  " set fileencodings=iso-2022-jp,ucs-bom,utf-8,euc-jp,cp932,default,latin1

  " encodingの値がUnicodeに設定された時のデフォルトの値を指定する
  set fileencodings=ucs-bom,utf-8,default,latin1
endif

if (v:version == 704 && has('patch785')) || v:version >= 705
  " 改行コードを勝手に付加しない
  set nofixeol
endif

if executable('rg')
  " rgコマンドを使用する場合の設定
  set grepprg=rg\ --hidden\ --no-heading\ --smart-case\ --vimgrep
  set grepformat=%f:%l:%c:%m,%f:%l:%m
elseif executable('pt')
  " ptコマンドを使用する場合の設定
  set grepprg=pt\ --nocolor\ --nogroup\ --smart-case
elseif has('win32') || has('win64')
  " grepコマンドでvimgrepを使用する
  set grepprg=internal
else
  " 外部grepを使用する場合の設定
  set grepprg=grep\ -inrEH
endif

if filereadable($VIMRUNTIME . '/macros/matchit.vim')
  " 標準添付されているmatchit.vimを読み込む
  source $VIMRUNTIME/macros/matchit.vim
endif

if has('nvim')
  colorscheme peachpuff
endif

if !has('nvim') && executable('locale') && system('locale -a') =~# 'ja_JP.UTF-8'
  " 表示を日本語にする
  language ja_JP.UTF-8
endif

" 改行コードをLFにする
set fileformat=unix

" defaults.vimで設定される値を上書きする
set scrolloff=0

" 日本語のヘルプを優先して表示する
set helplang=ja,en

" ファイルパスなどを最長補完して全てのマッチを表示する
set wildmode=list:longest

" vimgrepなどでディレクトリやファイルを除外する
if has('wildignore')
  set wildignore&
  " ディレクトリ
  set wildignore+=bower_components/**
  set wildignore+=node_modules/**
  set wildignore+=vendor/**
  " ドットから始まるディレクトリ
  set wildignore+=.git/**
  set wildignore+=.hg/**
  set wildignore+=.svn/**
  set wildignore+=.bundle/**
  set wildignore+=.sass-cache/**
  set wildignore+=.node-gyp/**
  set wildignore+=.cache/**
  " ファイル
  set wildignore+=*.exe,*.so,*.dll
  set wildignore+=*.bmp,*.gif,*.ico,*.jpg,*.jpeg,*.png,*.webp,*.ai,*.psd
endif

" tagsファイルを上位のディレクトリにさかのぼって検索する
set tags&
set tags+=./tags;

" https://www.micahsmith.com/blog/2019/11/fixing-vim-invalid-argument-diffopt-iwhite/
if &diff
  " set diffopt&
  set diffopt-=internal

  " diffsplitは常に垂直分割する
  set diffopt+=vertical

  " diffの空白を無視する
  set diffopt+=iwhite
endif

set incsearch   " インクリメンタルサーチ
set ignorecase  " 大文字小文字を区別しない
set smartcase   " 検索文字列に大文字を含む場合は区別する
set wrapscan    " 最後まで検索したら先頭に戻る
set hlsearch    " 検索文字列をハイライトする

set expandtab    " タブの代わりに半角スペースを挿入する

if has('smartindent')
  set smartindent  " 高度な自動インデント
endif

set tabstop=2      " タブ幅
set softtabstop=0  " 機能無効
set shiftwidth=2   " インデント幅

set nobomb  " BOMを付加しない
set hidden  " バッファを閉じないで非表示にする

" 候補が1つだけの場合もポップアップメニューを表示する
set completeopt&
set completeopt+=menuone,noinsert,noselect

" プレビューウィンドウを表示しない
set completeopt-=preview

" タブや改行などをを指定した記号で表示
" tab      タブ文字
" eol:     行末
" trail:   行末の半角スペース
" extends: 折り返しされた行の末尾
set listchars=tab:>.,eol:$,trail:_,extends:\

" バックスペースでいろいろ消せるように
set backspace=indent,eol,start

" シンタックスハイライトを300桁までに制限する
if has('syntax')
  set synmaxcol=300
endif

set number        " 行番号を表示する
set nowrap        " 折り返ししない
set showcmd       " コマンドを最下部に表示する
set shortmess&
set shortmess+=I  " 起動時のメッセージを表示しない

set ttyfast     " 高速ターミナル接続を行なう
"set lazyredraw  " キーボードから実行されないコマンドの実行で再描画しない

" 対応する括弧のハイライトを0.1秒にする
set matchtime=1

" <と>も強調表示対象に入れる
set matchpairs+=<:>

" 対応する開き括弧にわずかな時間ジャンプする
set showmatch

" 一部の全角文字を全角の幅で扱う
if exists('multi_byte')
  set ambiwidth=double
endif

" 折りたたみをインデントでする
" if has('folding')
"   set foldmethod=indent
" endif

" ステータスラインを常に表示する
set laststatus=2

" ステータスラインの表示を変更
if has('statusline')
  set statusline=%n\:%y%F\ \|%{(&fenc!=''?&fenc:&enc).'\|'.(&ff=='dos'?'crlf':&ff=='mac'?'cr':'lf').'\|'}%m%r%=<%l:%v>
endif

" コマンドラインの高さを2にする
set cmdheight=2

" バッファのディレクトリをカレントディレクトリにする
" if exists('+autochdir')
"   set autochdir
" endif

" mkdir関数が存在する場合
if exists('?mkdir')

  " バックアップファイルの保存先を変更 {{{
  if !isdirectory($HOME . '/.vim/backup')
    call mkdir($HOME . '/.vim/backup', 'p')
  endif

  set backupdir=~/.vim/backup,.,~/tmp,~/
  " }}}

  " スワップファイルの保存先を変更 {{{
  if !isdirectory($HOME . '/.vim/swap')
    call mkdir($HOME . '/.vim/swap', 'p')
  endif

  set directory=~/.vim/swap,.,~/tmp,/var/tmp,/tmp
  " }}}

  " アンドゥファイルの保存先を指定する {{{
  if !isdirectory($HOME . '/.vim/undo')
    call mkdir($HOME . '/.vim/undo', 'p')
  endif

  if has('persistent_undo')
    set undofile
    set undodir=~/.vim/undo,.,~/tmp,~/
  endif
  " }}}

endif

if exists(':Scratch') != 2
  command! -nargs=0 Scratch vertical new +setlocal\ buftype=nofile\ filetype=markdown\ noswapfile
endif

if executable('sudo') && executable('tee')
  " sudoを使って保存する
  command! -nargs=0 W execute 'w !sudo tee %'
endif

"-------------------------------------------------------------------------------
" from .vimrc.autocmd
"-------------------------------------------------------------------------------

" *.binと*.exeと*.dllはxxd
autocmd vimrc BufNewFile,BufRead *.{bin,exe,dll} setfiletype xxd

" *.cjsと*.jsと*.jsxと*.mjsと*.pacはJavaScript
autocmd vimrc BufNewFile,BufRead *.{cjs,js,jsx,mjs,pac} setfiletype javascript

" *.tsと*.tsxはTypeScript
autocmd vimrc BufNewFile,BufRead,VimEnter *.{ts,tsx} setfiletype typescript

if has('file_in_path')
  " gfで開く際に拡張子を補完する
  autocmd vimrc BufNewFile,BufRead *.{cjs,js,jsx,mjs,pac,ts,tsx} setlocal suffixesadd+=.tsx,.ts,.mjs,.cjs,.jsx,.js,.json,.pac
endif

" *.ftlはHTML
autocmd vimrc BufNewFile,BufRead *.ftl setfiletype html

" *.xulはXML
autocmd vimrc BufNewFile,BufRead *.xul setfiletype xml

function! s:set_filetype() abort
  " すでにfiletypeが指定されていたら終了
  if did_filetype()
    return
  endif

  let extension = expand('%:r:e')

  " 拡張子が取得できなかったら終了
  if empty(extension)
    return
  endif

  let filetypes = getcompletion(extension, 'filetype')

  " ファイルタイプが存在しなければ終了
  if empty(filetypes)
    return
  endif

  execute 'setlocal' 'filetype=' . filetypes[0]
endfunction

if exists('?getcompletion')
  autocmd vimrc BufNewFile,BufRead *.*.njk call <SID>set_filetype()
endif

" HTML編集時にシンタックスハイライトを400桁までに制限する
autocmd vimrc FileType html setlocal synmaxcol=400

" Makefile編集時のみタブにする
autocmd vimrc FileType make setlocal noexpandtab list tabstop=8 shiftwidth=8

" Python編集時のみインデントのスペース数を4にする
autocmd vimrc FileType python setlocal tabstop=4 shiftwidth=4

" Go編集時はタブにする
autocmd vimrc FileType go setlocal noexpandtab list tabstop=2 shiftwidth=2

" Markdown編集時のみインデントのスペース数を4にする
autocmd vimrc FileType markdown setlocal tabstop=4 shiftwidth=4

" QuickFixを開いた時に簡単にプレビューをしたりする
" https://thinca.hatenablog.com/entry/20130708/1373210009
autocmd vimrc FileType qf nnoremap <buffer> p <CR>zz<C-w>p

" CtrlP相当
autocmd vimrc FileType qf nnoremap <buffer> <C-n> j
autocmd vimrc FileType qf nnoremap <buffer> <C-p> k

" netrwでeやlを押したらCRと同じ動きにする
autocmd vimrc FileType netrw nmap <buffer> e <CR>
autocmd vimrc FileType netrw nmap <buffer> l <CR>

" netrwでhを押したら-と同じ動きにする
autocmd vimrc FileType netrw nmap <buffer> h -

" netrwでqを押したら閉じる
autocmd vimrc FileType netrw nnoremap <buffer><nowait><silent> q :<C-u>bwipeout!<CR>

" netrwでtを押したらツリーを閉じる
autocmd vimrc FileType netrw nmap <buffer><silent> t <Plug>NetrwTreeSqueeze

" netrwで<Space>を押したらマークする
autocmd vimrc FileType netrw nmap <buffer><expr> <Space> getline('.') == getline('$') ? 'mf' : 'mfj'

" netrwでMを押したらマークを外す
autocmd vimrc FileType netrw nmap <buffer> M mF

" netrwでドットファイルの表示・非表示を切り替える
autocmd vimrc FileType netrw nmap <buffer> . gh

" netrwで新しいファイルを作る
autocmd vimrc FileType netrw nmap <buffer> N %

" netrwで新しいディレクトリを作る
autocmd vimrc FileType netrw nmap <buffer> K d

" netrwで$HOMEに移動する
autocmd vimrc FileType netrw nmap <buffer><silent> ~ :<C-u>Explore $HOME<CR>

" netrwで/に移動する
autocmd vimrc FileType netrw nmap <buffer><silent> \ :<C-u>Explore /<CR>

" 挿入モードを開始したときにペーストモードのキーバインドを設定する
autocmd vimrc InsertEnter * set pastetoggle=<C-t>

" 挿入モードから抜けるときにペーストモードを抜け、キーバインドも解除する
autocmd vimrc InsertLeave * set nopaste pastetoggle=

" コメント中の特定の単語を強調表示する
autocmd vimrc WinEnter,WinLeave,BufRead,BufNew * call matchadd('Todo', '\<\%(TODO\|FIXME\|CHANGED\|XXX\|BUG\|HACK\|NOTE\|INFO\|IDEA\)\>')

" ウィンドウを移動したらバッファ番号とフルパスを表示する
autocmd vimrc WinEnter * execute 'normal! 2\<C-g>'

" 全角スペースに下線を引く
highlight FullWidthSpace cterm=underline ctermfg=Blue
autocmd vimrc WinEnter,WinLeave,BufRead,BufNew * match FullWidthSpace /　/

" Go編集時にerrをハイライトする
" http://yuroyoro.hatenablog.com/entry/2014/08/12/144157
highlight goHighlight cterm=bold ctermfg=214
autocmd vimrc FileType go call matchadd('goHighlight', '\<\%(_\|err\)\>')

" Go編集時に末尾のセミコロンをハイライトする
highlight goSemicolon cterm=bold ctermfg=White ctermbg=Red
autocmd vimrc FileType go call matchadd('goSemicolon', ';\ze\s*$')

" Jekyllのための保存時刻自動入力設定
" https://jekyllrb.com/docs/frontmatter/
"function! s:set_autodate_for_jekyll()
"  " バッファローカルなautodate.vimの設定
"  " http://nanasi.jp/articles/vim/autodate_vim.html
"  let b:autodate_lines = 5
"  let b:autodate_keyword_pre = 'date: '
"  let b:autodate_keyword_post = '$'
"  let b:autodate_format = '%Y-%m-%d %H:%M:%S'
"endfunction

" Markdownファイルを開いたときにだけ実行する
"autocmd vimrc BufNewFile,BufRead *.{md,markdown,mkd,mdown,mkdn,mark} call s:set_autodate_for_jekyll()

" いくつかの拡張子をMarkdownとして開く
autocmd vimrc BufNewFile,BufRead *.{md,markdown,mkd,mdown,mkdn,mark} setfiletype markdown

" JavaScriptの著名なモジュールの設定ファイルをJSONとして開く
autocmd vimrc BufNewFile,BufRead .{babel,eslint,stylelint,swc,textlint}rc setfiletype json

" make,vimgrep,vimgrepaddを実行したらcopenをする
" grep,grepaddはCtrlPQuickfixを使用する
autocmd vimrc QuickfixCmdPost make,vimgrep,vimgrepadd copen

" vimdiffなどdiffをするときはカラースキームを変更する
" https://stackoverflow.com/a/2019401
autocmd vimrc FilterWritePre * if &diff | colorscheme industry | endif

" ターミナルを開いたら行番号を表示しない
if has('nvim')
  autocmd vimrc TermOpen * setlocal nonumber norelativenumber
elseif has('terminal') && has('patch-8.1.2219')
  autocmd vimrc TerminalWinOpen * setlocal nonumber norelativenumber
endif

" ターミナルを開いたら挿入モードにする
if has('nvim')
  autocmd vimrc TermOpen * startinsert
endif

"-------------------------------------------------------------------------------
" from .vimrc.map
"-------------------------------------------------------------------------------

" 論理行でなく表示行で移動する
nnoremap <silent> j gj
vnoremap <silent> j gj

nnoremap <silent> k gk
vnoremap <silent> k gk

"nnoremap <silent> $ g$
"vnoremap <silent> $ g$

"nnoremap <silent> ^ g^
"vnoremap <silent> ^ g^

"nnoremap <silent> 0 g0
"vnoremap <silent> 0 g0

" very magicをonにする
" http://deris.hatenablog.jp/entry/2013/05/15/024932
nnoremap / /\v
nnoremap ? ?\v

" / を入力した際の挙動を条件によって変更する
function! s:get_smart_very_magic() abort
  if getcmdtype() !=# ':'
    return '/'
  endif

  let cmd = getcmdline()
  let pos = getcmdpos()

  if
        \ (cmd ==#      's' && pos == 2) ||
        \ (cmd ==#     '%s' && pos == 3) ||
        \ (cmd ==# "'<,'>s" && pos == 7)
    " :s/ と入力したら :s/\v にする
    return '/\v'
  elseif
        \ (cmd ==#        's/\v' && pos ==  5) ||
        \ (cmd ==#       '%s/\v' && pos ==  6) ||
        \ (cmd ==# '''<,''>s/\v' && pos == 10)
    " :s// と入力したら :s/\v/ から :s// にする
    return "\<BS>\<BS>/"
  endif

  return '/'
endfunction

" very magicをonにする
cnoremap <expr> / <SID>get_smart_very_magic()

" diff系
nnoremap ,dg :<C-u>diffget<CR>
nnoremap ,do :<C-u>diffoff<CR>
nnoremap ,dO :<C-u>diffoff!<CR>
nnoremap ,dt :<C-u>diffthis<CR>
nnoremap ,du :<C-u>diffupdate<CR>

" F1を無効化
noremap <F1> <Nop>
noremap! <F1> <Nop>

" (, )でバッファの移動
nnoremap <silent> ( :<C-u>bprevious<CR>
nnoremap <silent> ) :<C-u>bnext<CR>

" バッファ番号とフルパスを表示する
nnoremap <C-g> 2<C-g>

" コマンドラインモードでEmacs風の移動ができるようにする
" http://cohama.hateblo.jp/entry/20130529/1369843236
cnoremap <C-a> <Home>
cnoremap <C-b> <Left>
cnoremap <C-d> <Del>
cnoremap <C-e> <End>
cnoremap <C-f> <Right>
cnoremap <C-n> <Down>
cnoremap <C-p> <Up>
" cnoremap <M-b> <S-Left>
" cnoremap <M-f> <S-Right>

" grepをする
" based on https://zenn.dev/skanehira/articles/2020-09-18-vim-cexpr-quickfix
function! s:grep(word) abort
  cexpr system(printf('%s "%s"', &grepprg, a:word)) | cwin
endfunction
command! -nargs=1 Grep call <SID>grep(<q-args>)
nnoremap ,gr :<C-u>Grep<Space>

" C-kでオムニ補完
inoremap <C-k> <C-x><C-o>

" Tabで補完時に次の候補へ移動
inoremap <expr> <Tab> pumvisible() ? "\<C-n>" : "\<Tab>"

" S-Tabで補完時に前の候補へ移動
inoremap <expr> <S-Tab> pumvisible() ? "\<C-p>" : "\<S-Tab>"

" Enterで補完時に決定
inoremap <expr> <CR> pumvisible() ? "\<C-y>" : "\<CR>"

if (exists('*dein#tap') && !dein#tap('vfiler.vim')) && 1
  if has('terminal') && executable('vifm')
    function! s:open_vifm() abort
      let terminal = [
            \   'terminal',
            \   '++close',
            \ ]

      " ウィンドウが2つ以上存在する場合はそのウィンドウで開く
      " ウィンドウが1つのときにVifmを終了するとVimも終了してしまうため
      if winnr('$') > 1
        call add(terminal, '++curwin')
      endif

      let command = [
            \   'vifm',
            \   expand('%:p:h'),
            \   expand('%:p:h'),
            \ ]

      " error from vint: unexpected token: -> (see vim-jp/vim-vimlparser)
      " execute terminal->join(' ') command->join(' ')
      execute join(terminal, ' ') join(command, ' ')
    endfunction

    nnoremap <silent> ,vf :<C-u>call <SID>open_vifm()<CR>
    nnoremap <silent> ,vF :<C-u>call <SID>open_vifm()<CR>
  else
    " ,vfでnetrwを開く
    function! s:open_netrw() abort
      " ls -lのような表示にする
      " 長いディレクトリが開けなくなるので付加情報を表示しない
      " let g:netrw_liststyle = 1
      let g:netrw_liststyle = 0
      " netrw
      Explore
    endfunction
    nnoremap <silent> ,vf :<C-u>call <SID>open_netrw()<CR>

    " ,vFでサイドバー風にnetrwを開く
    function! s:open_netrw_by_side() abort
      " ツリー表示にする
      let g:netrw_liststyle = 3
      " netrw
      Lexplore
    endfunction
    nnoremap <silent> ,vF :<C-u>call <SID>open_netrw_by_side()<CR>
  endif
endif

" QuickFixを更新する
" via: https://vi.stackexchange.com/a/13663
function! s:update_quickfix() abort
  let list = map(
        \   getqflist(),
        \   'extend(v:val, { "text" : get(getbufline(v:val.bufnr, v:val.lnum), 0) })'
        \ )
  call setqflist(list)
endfunction
command! -nargs=0 UpdateQuickFix call <SID>update_quickfix()

if has('terminal') || has('nvim')
  " ターミナルを開く
  nnoremap <silent> ,t :<C-u>terminal<CR>
  nnoremap <silent> ,T :<C-u>vertical terminal<CR>
endif

" vim:ft=vim:fdm=marker:fen:
