scriptencoding utf-8

" TODO: http://www.kawaz.jp/pukiwiki/?vim#cb691f26

" Lua {{{

" luaコマンドが使用できないがluadllが指定できluajitコマンドが使用できる場合
if !has('lua') && exists('&luadll') && executable('luajit')
  if !empty($HOMEBREW_DIR)
    let s:lua = $HOMEBREW_DIR . '/opt/luajit/lib/libluajit.dylib'
  else
    let s:lua = $HOME . '/Homebrew/opt/luajit/lib/libluajit.dylib'
  endif

  if filereadable(s:lua)
    execute 'set luadll=' . s:lua
  endif

  unlet s:lua
endif

" }}}

" Python3 {{{

if has('python3_compiled') && has('python3_dynamic') && exists('&pythonthreedll')
  if !empty($HOMEBREW_DIR)
    let s:python3_dir = $HOMEBREW_DIR . '/opt/python@3'
    let s:python3_bin = s:python3_dir . '/bin/python'
  else
    let s:python3_dir = $HOME . '/Homebrew/opt/python@3'
    let s:python3_bin = s:python3_dir . '/bin/python'
  endif

  if executable(s:python3_bin)
    let $PYTHONHOME = s:python3_dir
    execute 'set pythonthreedll=' . s:python3_bin

    if has('python3')
      python3 import sys
    endif
  endif

  unlet s:python3_bin
  unlet s:python3_dir
endif

" }}}

" グループの初期化 {{{

augroup vimrc
  autocmd!
augroup END

" }}}

" 分割した設定ファイルを読み込む {{{

" symlink先のファイルパスを解決
let s:vimrc_file = resolve($MYVIMRC)
" ファイル名を取り除く
let s:vimrc_dir = fnamemodify(s:vimrc_file, ':h')

execute 'source' s:vimrc_dir . '/.vimrc.plugin'
execute 'source' s:vimrc_dir . '/.vimrc.config'
execute 'source' s:vimrc_dir . '/.vimrc.autocmd'
execute 'source' s:vimrc_dir . '/.vimrc.map'

unlet s:vimrc_file
unlet s:vimrc_dir

" }}}

" 環境固有の設定を読み込む {{{

let s:vimrc_local = $HOME . '/.vimrc.local'

if filereadable(s:vimrc_local)
  execute 'source' s:vimrc_local
endif

unlet s:vimrc_local

" }}}

set secure

" vim:ft=vim:fdm=marker:fen:
